apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "org.junit.platform.gradle.plugin"
apply plugin: "com.jfrog.artifactory"
apply plugin: "com.jfrog.bintray"

group = "com.betterconfig"
version = "${version}" + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext{
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath ("org.junit.platform:junit-platform-gradle-plugin:1.1.0")
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: "com.squareup.okhttp3", name: "okhttp", version:"3.9.1"
    compile group: "org.slf4j", name: "slf4j-api", version:"1.7.25"
    compile group: "com.google.guava", name: "guava", version:"24.0-jre"
    compile group: "com.google.code.gson", name: "gson", version:"2.8.2"
    testCompile group: "org.junit.jupiter", name: "junit-jupiter-api", version:"5.1.0"
    testCompile group: "org.slf4j", name: "slf4j-nop", version:"1.7.25"
    testCompile group: "com.squareup.okhttp3", name: "mockwebserver", version:"3.7.0"
    testRuntime group: "org.junit.jupiter", name: "junit-jupiter-engine", version:"5.0.1"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    description "BetterConfig Java client library."
    name "BetterConfig Java Client"
    url "https://github.com/betterconfig/betterconfigclient-java"

    scm {
        url "https://github.com/betterconfig/betterconfigclient-java"
        developerConnection "scm:git:ssh:git@github.com:betterconfig/betterconfigclient-java.git"
        connection "scm:git:git://github.com/betterconfig/betterconfigclient-java.git"
    }

    licenses {
        license {
            name "MIT License"
            url "https://raw.githubusercontent.com/betterconfig/betterconfigclient-java/master/LICENSE"
        }
    }

    organization {
        url "https://betterconfig.com"
        name "BetterConfig"
    }

    developers {
        developer {
            id "betterconfig"
            email "developer@betterconfig.com"
            name "BetterConfig"
        }
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

artifactory {
    contextUrl = "http://oss.jfrog.org"
    publish {
        repository {
            repoKey = "oss-snapshot-local"
            username = bintrayUser
            password = bintrayKey
        }
        defaults {
            publications("mavenPublication")
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = "jcenter"
    }
    clientConfig.info.setBuildNumber(buildNumber)
    clientConfig.info.setBuildName("betterconfig")
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ["mavenPublication"]

    pkg {
        repo = "releases"
        name = "betterconfig-client"
        desc = "BetterConfig Java client library."
        userOrg = "better-config"
        licenses = ["MIT"]
        websiteUrl = "https://betterconfig.com"
        vcsUrl = "https://github.com/betterconfig/betterconfigclient-java"
        githubRepo = "https://github.com/betterconfig/betterconfigclient-java"
        labels = ["betterconfig", "featureflag"]
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = "build ${buildNumber}"
            released  = new Date()
        }
    }
}